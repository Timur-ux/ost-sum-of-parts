"Multiplies 2 numbers. Naive variant based on continious summing"
MT MULT;
	BEGIN
	ALPHABET: 0,1,2,3,4,5,6,7,8,9,T,F;
	MT BNORM; LIB;
	MT ENORM; LIB;
	MT SUM; LIB;
	MT L; LIB;
	MT R; LIB;
	MT K3; LIB;
	MT K4; LIB;

	MT ISZERO;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9, T, F;
		l;
		IF
			_ ? R; r; a(T); r;
			0 ? ISZERO;
		ELSE R; r; a(F); r; FI;
	END ISZERO;

	MT DECREASE; "Destructable"
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			1 ? a(0);
			2 ? a(1);
			3 ? a(2);
			4 ? a(3);
			5 ? a(4);
			6 ? a(5);
			7 ? a(6);
			8 ? a(7);
			9 ? a(8);
			0 ? a(9); DECREASE;
		FI;
		R;
	END DECREASE;

	BNORM; r; a(0) r;
	K4**2; "..._#_acc_w1_w2_"
	ISZERO; l;
	DO F ?
		a(_); l; DECREASE; BNORM; K4**2; "_acc_w1_(w2-1)_#_acc_w1_"
		SUM; L; ENORM; "_acc_w1_(w2-1)_(acc + w1 = newacc)_"
		K3**2; "..._newacc_w1_(w2-1)_"
		ISZERO; l;
	OD; "..._acc_w1_0_T"
	a(_); l; K3; "..._acc_w1_0_acc_"
	L; ENORM;
END MULT
