"Multiplies 2 numbers"
MT MULT;
	BEGIN
	ALPHABET: 0,1,2,3,4,5,6,7,8,9,T,F;
	MT BNORM; LIB;
	MT ENORM; LIB;
	MT SUM; LIB;
	MT L; LIB;
	MT R; LIB;
	MT K3; LIB;
	MT K4; LIB;
	MT K2; LIB;
	MT K5; LIB;
	MT K6; LIB;
	MT K7; LIB;
	MT SHIFTRIGHT; LIB;
	MT SHIFTLEFT; LIB;
	MT ISLESS; LIB;

	MT CARRY1;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(1)
			1 ? a(2)
			2 ? a(3)
			3 ? a(4)
			4 ? a(5)
			5 ? a(6)
			6 ? a(7)
			7 ? a(8)
			8 ? a(9)
			9 ? a(0); CARRY1
			_ ? SHIFTRIGHT; a(1);
		FI;
	END CARRY1;

	MT CARRY2;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(2)
			1 ? a(3)
			2 ? a(4)
			3 ? a(5)
			4 ? a(6)
			5 ? a(7)
			6 ? a(8)
			7 ? a(9)
			8 ? a(0)
			9 ? a(1); CARRY1
			_ ? SHIFTRIGHT; a(2);
		FI;
	END CARRY2;

	MT CARRY3;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(3)
			1 ? a(4)
			2 ? a(5)
			3 ? a(6)
			4 ? a(7)
			5 ? a(8)
			6 ? a(9)
			7 ? a(0)
			8 ? a(1)
			9 ? a(2); CARRY1
			_ ? SHIFTRIGHT; a(3);
		FI;
	END CARRY3;

	MT CARRY4;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(4)
			1 ? a(5)
			2 ? a(6)
			3 ? a(7)
			4 ? a(8)
			5 ? a(9)
			6 ? a(0)
			7 ? a(1)
			8 ? a(2)
			9 ? a(3); CARRY1
			_ ? SHIFTRIGHT; a(4);
		FI;
	END CARRY4;

	MT CARRY5;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(5)
			1 ? a(6)
			2 ? a(7)
			3 ? a(8)
			4 ? a(9)
			5 ? a(0)
			6 ? a(1)
			7 ? a(2)
			8 ? a(3)
			9 ? a(4); CARRY1
			_ ? SHIFTRIGHT; a(5);
		FI;
	END CARRY5;

	MT CARRY6;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(6)
			1 ? a(7)
			2 ? a(8)
			3 ? a(9)
			4 ? a(0)
			5 ? a(1)
			6 ? a(2)
			7 ? a(3)
			8 ? a(4)
			9 ? a(5); CARRY1
			_ ? SHIFTRIGHT; a(6);
		FI;
	END CARRY6;

	MT CARRY7;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(7)
			1 ? a(8)
			2 ? a(9)
			3 ? a(0)
			4 ? a(1)
			5 ? a(2)
			6 ? a(3)
			7 ? a(4)
			8 ? a(5)
			9 ? a(6); CARRY1
			_ ? SHIFTRIGHT; a(7);
		FI;
	END CARRY7;

	MT CARRY8;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(8)
			1 ? a(9)
			2 ? a(0)
			3 ? a(1)
			4 ? a(2)
			5 ? a(3)
			6 ? a(4)
			7 ? a(5)
			8 ? a(6)
			9 ? a(7); CARRY1
			_ ? SHIFTRIGHT; a(8);
		FI;
	END CARRY8;

	MT CARRY9;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(9)
			1 ? a(0)
			2 ? a(1)
			3 ? a(2)
			4 ? a(3)
			5 ? a(4)
			6 ? a(5)
			7 ? a(6)
			8 ? a(7)
			9 ? a(8); CARRY1
			_ ? SHIFTRIGHT; a(9);
		FI;
	END CARRY9;

	MT MULTBYDIGIT; "Destructable"
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
		 0 ? a(_); l**2; DO () != _ ? a(_); l; OD; r; a(0)
		 1 ? a(_); l
		 2 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(2)
					2 ? a(4)
					3 ? a(6)
					4 ? a(8)
					5 ? a(0); CARRY1
					6 ? a(2); CARRY1
					7 ? a(4); CARRY1
					8 ? a(6); CARRY1
					9 ? a(8); CARRY1
				FI;
				l;
			OD;
		 3 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(3)
					2 ? a(6)
					3 ? a(9)
					4 ? a(2)
					5 ? a(5); CARRY1
					6 ? a(8); CARRY1
					7 ? a(1); CARRY2
					8 ? a(4); CARRY2
					9 ? a(7); CARRY2
				FI;
				l;
			OD;
		 4 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(4)
					2 ? a(8)
					3 ? a(2); CARRY1
					4 ? a(6); CARRY1
					5 ? a(0); CARRY2
					6 ? a(4); CARRY2
					7 ? a(8); CARRY2
					8 ? a(2); CARRY3
					9 ? a(6); CARRY3
				FI;
				l;
			OD;
		 5 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(5)
					2 ? a(0); CARRY1
					3 ? a(5); CARRY1
					4 ? a(0); CARRY2
					5 ? a(5); CARRY2
					6 ? a(0); CARRY3
					7 ? a(5); CARRY3
					8 ? a(0); CARRY4
					9 ? a(5); CARRY4
				FI;
				l;
			OD;
		 6 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(6)
					2 ? a(2); CARRY1
					3 ? a(8); CARRY1
					4 ? a(4); CARRY2
					5 ? a(0); CARRY3
					6 ? a(6); CARRY3
					7 ? a(2); CARRY4
					8 ? a(8); CARRY4
					9 ? a(4); CARRY5
				FI;
				l;
			OD;
		 7 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(7)
					2 ? a(4); CARRY1
					3 ? a(1); CARRY2
					4 ? a(8); CARRY2
					5 ? a(5); CARRY3
					6 ? a(2); CARRY4
					7 ? a(9); CARRY4
					8 ? a(6); CARRY5
					9 ? a(3); CARRY6
				FI;
				l;
			OD;
		 8 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(8)
					2 ? a(6); CARRY1
					3 ? a(4); CARRY2
					4 ? a(2); CARRY3
					5 ? a(0); CARRY4
					6 ? a(8); CARRY4
					7 ? a(6); CARRY5
					8 ? a(4); CARRY6
					9 ? a(2); CARRY7
				FI;
				l;
			OD;
		 9 ? a(_); l**2;
			DO () != _ ? 
				IF
					1 ? a(9)
					2 ? a(8); CARRY1
					3 ? a(7); CARRY2
					4 ? a(6); CARRY3
					5 ? a(5); CARRY4
					6 ? a(4); CARRY5
					7 ? a(3); CARRY6
					8 ? a(2); CARRY7
					9 ? a(1); CARRY8
				FI;
				l;
			OD;
		FI;
		R; DO _ ? l; OD; r;
	END MULTBYDIGIT;

	MT GETLASTDIGIT;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(_); r; SHIFTLEFT; r; a(0); r;
			1 ? a(_); r; SHIFTLEFT; r; a(1); r;
			2 ? a(_); r; SHIFTLEFT; r; a(2); r;
			3 ? a(_); r; SHIFTLEFT; r; a(3); r;
			4 ? a(_); r; SHIFTLEFT; r; a(4); r;
			5 ? a(_); r; SHIFTLEFT; r; a(5); r;
			6 ? a(_); r; SHIFTLEFT; r; a(6); r;
			7 ? a(_); r; SHIFTLEFT; r; a(7); r;
			8 ? a(_); r; SHIFTLEFT; r; a(8); r;
			9 ? a(_); r; SHIFTLEFT; r; a(9); r;
		FI;
	END GETLASTDIGIT;

	MT ISZERO;
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9, T, F;
		l;
		IF
			_ ? R; r; a(T); r;
			0 ? ISZERO;
		ELSE R; r; a(F); r; FI;
	END ISZERO;

	MT DECREASE; "Destructable"
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			1 ? a(0);
			2 ? a(1);
			3 ? a(2);
			4 ? a(3);
			5 ? a(4);
			6 ? a(5);
			7 ? a(6);
			8 ? a(7);
			9 ? a(8);
			0 ? a(9); DECREASE;
		FI;
		R;
	END DECREASE;

	MT INCREASE; "Destructable"
		BEGIN
		ALPHABET: 0,1,2,3,4,5,6,7,8,9;
		l;
		IF
			0 ? a(1);
			1 ? a(2);
			2 ? a(3);
			3 ? a(4);
			4 ? a(5);
			5 ? a(6);
			6 ? a(7);
			7 ? a(8);
			8 ? a(9);
			9 ? a(0); INCREASE;
			_ ? SHIFTRIGHT; a(1);
		FI;
		R;
	END INCREASE;

	BNORM;
	r; a(0); r**2; a(0); r;
	K5**2;
	l; 
	DO () != _ ? "_acc_ord_w1_w2d_"
		r; BNORM; "_acc_ord_w1_w2d_#_"
		L; GETLASTDIGIT; "_acc_ord_w1_w2_#_d_"
		K4; K2; "_acc_ord_w1_w2_#_d_w1_d_"
		MULTBYDIGIT; "_acc_ord_w1_w2_#_d_(w1xd)_"
		K6; "_acc_w1_w2_#_d_(w1xd)_ord_"
		ISZERO; l; "..._#_d_(w1xd)_ord_(T|F)"
		DO
			F ? a(_); l; DECREASE; L; SHIFTRIGHT; l; a(0); r; R; ISZERO; l;
		OD; 
		a(_); l; "..._#_d_(w1xd)0...ord times...0_0_"
		l; DO () != _ ? a(_); l; OD; "_acc_ord_w1_w2_#_d_(w1xw2)0...0_"
		K7; "_acc_ord_w1_w2_#_d_(w1xw2)0...0_acc_"

		SUM; "_acc_ord_w1_w2_#_d_(w1xd)0...0_acc_newacc_"
		L; ENORM;  "_acc_ord_w1_w2_newacc_"
		K4 ** 3;  "_acc_ord_w1_w2_newacc_ord_w1_w2_"
		L**2; INCREASE; R**2; "_acc_ord_w1_w2_newacc_neword_w1_w2_"
		l;
	OD; "..._acc_ord_w1__"
	l; K3; L; ENORM; "_w1_w2_(w1xw2)"
END MULT
